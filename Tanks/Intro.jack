class Intro
{
    static Array prehistoryLines, companyNameLines, sponsors;
    static int blackScreenWaitingMS;
    function void init() {
        var TextLine textLineBuf;
        let blackScreenWaitingMS = 750;

        let prehistoryLines = Array.new(10);
        let prehistoryLines[0] = "I vosstali studenti iz pepla uchebnih zadach.";
        let prehistoryLines[1] = "I nachalas razrabotka v paradigme extremalnogo programirovania.";
        let prehistoryLines[2] = "I dlilas' ona nedelyami.";
        let prehistoryLines[3] = "No poslednaya strochka coda budet napisana ne v budushem.";
        let prehistoryLines[4] = "Mi radi predstavit nashu igru pryamo seychas.";

        let companyNameLines = Array.new(2);
        let textLineBuf = TextLine.new("FIREGAMES", 112, 67);
        let companyNameLines[0] = textLineBuf;
        let textLineBuf = TextLine.new("STUDIO", 160, 122);
        let companyNameLines[1] = textLineBuf;

        let sponsors = Array.new(3);
        let sponsors[0] = "Coffee";
        let sponsors[1] = "Naive self-confidence";
        let sponsors[2] = "Youthful maximalism";
        return;
    }
    function void exec() {
        do OutputExtension.init();
        do Intro.showPrehistory();
        do Intro.showCompany();
        do Intro.showSponsors();
        do Intro.showAwards();
        do GameCore.setState(1);
        do OutputExtension.dispose();
        return;
    }

    function void showPrehistory() {
        var int i, j, currentPrehistoryLineLength, cursorRectangleX, cursorRectangleY; 
        var String currentPrehistoryLine;
        do Graphics.fillScreen(0);
        do Screen.setColor(1);
        let i = 0;
        let cursorRectangleX = 0;
        let cursorRectangleY = 0;

        while (i < 5) {
            let j = 0;
            do Output.moveCursor(i, j);
            do Intro.showCursor(cursorRectangleX, cursorRectangleY, true);
            let currentPrehistoryLine = prehistoryLines[i];
            let currentPrehistoryLineLength = currentPrehistoryLine.length();

            while (j < currentPrehistoryLineLength) {
                do Intro.showCursor(cursorRectangleX, cursorRectangleY, false);
                do Output.printChar(currentPrehistoryLine.charAt(j));
                if (KeyboardExtension.keyDown()) { return; }
                let j = j + 1;
                let cursorRectangleX = cursorRectangleX + 8;

                do Intro.showCursor(cursorRectangleX, cursorRectangleY, true);
                do Sys.wait(50);
            }

            do Intro.showCursor(cursorRectangleX, cursorRectangleY, false);
            let cursorRectangleX = 0;
            let cursorRectangleY = cursorRectangleY + 11;
            let i = i + 1;
        }
        do Intro.blinkCursor(3, cursorRectangleX, cursorRectangleY);
        return;
    }

    function void showCompany() {
        var int i, j, lengthBuf;
        var int x_start;
        var TextLine companyNameLine;
        var String stringBuf;

        do Graphics.fillScreen(1);
        do Sys.wait(blackScreenWaitingMS);
        do Graphics.fillScreen(0);

        let i = 0;
        while (i < 2) {
            if (KeyboardExtension.keyDown()) { return; }
            let companyNameLine = companyNameLines[i];
            let stringBuf = companyNameLine.data();
            let lengthBuf = stringBuf.length();

            do OutputExtension.setCursorPosition(companyNameLine.x(), companyNameLine.y());
            let j = 0;
            while (j < lengthBuf) {
                do OutputExtension.printChar(stringBuf.charAt(j));
                let j = j + 1;
            }
            let i = i + 1;
        }

        if (GameCore.interactiveWait(750))  { return; }

        let stringBuf = "PRESENT";
        let x_start = 220;
        while (x_start < 290) {
            do Output.moveCursor(17, 28);
            do Output.printString(stringBuf);
            do Screen.setColor(0);
            do Screen.drawRectangle(x_start, 185, 290, 201);
            let x_start = x_start + 1;
            if (KeyboardExtension.keyDown()) { return; }
            do Sys.wait(25);
        }    

        if (GameCore.interactiveWait(1000))  { return; }    
        return;
    }

    function void showSponsors() {
        var int i, cursorI, cursorJ, circleY;
        var String buf;

        do Graphics.fillScreen(1);
        do Sys.wait(blackScreenWaitingMS);
        do Graphics.fillScreen(0);

        do Screen.setColor(1);
        let cursorI = 13;
        let cursorJ = 21;
        let circleY = 148;
        do OutputExtension.setCursorPosition(64, 87);
        do OutputExtension.printString("SPONSORED BY");

        let i = 0;
        while (i < 3) {
            do Screen.drawCircle(160, circleY, 3);
            do Output.moveCursor(cursorI, cursorJ);
            do Output.printString(sponsors[i]);
            let cursorI = cursorI + 1;
            let circleY = circleY + 11;
            let i = i + 1;
        }

        do Output.moveCursor(21, 21);
        do Output.printString("C  All rights reserved");
        do ScreenExtension.drawHollowCircle(171, 236, 7);
        if (GameCore.interactiveWait(5000)) { return; }
        return;
    }
    
    function void showAwards() {
        
        do Graphics.fillScreen(1);
        do Sys.wait(blackScreenWaitingMS);
        do Graphics.fillScreen(0);

        do Output.moveCursor(11, 10);
        do Output.printString("AWARD WINNER 'VAM CHTO NECHEGO DELAT?' 2024");
        if (GameCore.interactiveWait(5000))  { return; }
        return;
    }

    function void showCursor(int x, int y, boolean color) {
        do Screen.setColor(color);
        do Screen.drawRectangle(x, y, x + 7, y + 10);
        return;
    }

    function void blinkCursor(int times, int cursorRectangleX, int cursorRectangleY) {
        var int i;
        let i = 0;
        while (i < times) {
            do Intro.showCursor(cursorRectangleX, cursorRectangleY, true);
            do Sys.wait(500);
            do Intro.showCursor(cursorRectangleX, cursorRectangleY, false);
            do Sys.wait(500);
            let i = i + 1;
        }
        return;
    }
}