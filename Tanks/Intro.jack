class Intro
{
    static Array prehistoryLines, companyNameLines;
    function void init() {
        var TextLine textLineBuf;
        let prehistoryLines = Array.new(10);
        let prehistoryLines[0] = "I vosstali studenti iz pepla uchebnih zadach.";
        let prehistoryLines[1] = "I nachalas razrabotka v paradigme extremalnogo programirovania.";
        let prehistoryLines[2] = "I dlilas' ona nedelyami.";
        let prehistoryLines[3] = "No poslednaya strochka coda budet napisana ne v budushem.";
        let prehistoryLines[4] = "Mi radi predstavit nashu igru pryamo seychas.";

        let companyNameLines = Array.new(2);
        let textLineBuf = TextLine.new("FIREGAMES", 112, 67);
        let companyNameLines[0] = textLineBuf;
        let textLineBuf = TextLine.new("STUDIO", 160, 122);
        let companyNameLines[1] = textLineBuf;
        return;
    }
    function void exec() {
        //do Intro.showPrehistory();
        do Intro.showCompany();
        return;
    }

    function void showPrehistory() {
        var int i, j, currentPrehistoryLineLength, cursorRectangleX, cursorRectangleY; 
        var String currentPrehistoryLine;
        let i = 0;
        let cursorRectangleX = 0;
        let cursorRectangleY = 0;

        while (i < 5) {
            let j = 0;
            do Output.moveCursor(i, j);
            do Intro.showCursor(cursorRectangleX, cursorRectangleY, true);
            let currentPrehistoryLine = prehistoryLines[i];
            let currentPrehistoryLineLength = currentPrehistoryLine.length();

            while (j < currentPrehistoryLineLength) {
                do Intro.showCursor(cursorRectangleX, cursorRectangleY, false);
                do Output.printChar(currentPrehistoryLine.charAt(j));
                if (Keyboard.keyPressed())  { return; }
                let j = j + 1;
                let cursorRectangleX = cursorRectangleX + 8;

                do Intro.showCursor(cursorRectangleX, cursorRectangleY, true);
                do Sys.wait(50);
            }

            do Intro.showCursor(cursorRectangleX, cursorRectangleY, false);
            let cursorRectangleX = 0;
            let cursorRectangleY = cursorRectangleY + 11;
            let i = i + 1;
        }
        do Intro.blinkCursor(3, cursorRectangleX, cursorRectangleY);
        return;
    }

    function void showCompany() {
        var int i, j, lengthBuf;
        var int x_start;
        var TextLine companyNameLine;
        var String stringBuf;

        do OutputExtension.init();
        do Graphics.fillScreen(0);

        let i = 0;
        while (i < 2) {
            if (Keyboard.keyPressed())  { return; }
            let companyNameLine = companyNameLines[i];
            let stringBuf = companyNameLine.data();
            let lengthBuf = stringBuf.length();

            do OutputExtension.setCursorPosition(companyNameLine.x(), companyNameLine.y());
            let j = 0;
            while (j < lengthBuf) {
                do OutputExtension.printChar(stringBuf.charAt(j));
                let j = j + 1;
            }
            let i = i + 1;
        }

        if (GameCore.interactiveWait(750))  { return; }

        let stringBuf = "PRESENT";
        let x_start = 220;
        while (x_start < 290) {
            do Output.moveCursor(17, 28);
            do Output.printString(stringBuf);
            do Screen.setColor(0);
            do Screen.drawRectangle(x_start, 185, 290, 201);
            let x_start = x_start + 1;
            if (Keyboard.keyPressed())  { return; }
            do Sys.wait(25);
        }    

        if (GameCore.interactiveWait(1000))  { return; }    
        return;
    }
    
    function void showCursor(int x, int y, boolean color) {
         do Screen.setColor(color);
         do Screen.drawRectangle(x, y, x + 7, y + 10);
         return;
    }

    function void blinkCursor(int times, int cursorRectangleX, int cursorRectangleY) {
        var int i;
        let i = 0;
        while (i < times) {
            do Intro.showCursor(cursorRectangleX, cursorRectangleY, true);
            do Sys.wait(500);
            do Intro.showCursor(cursorRectangleX, cursorRectangleY, false);
            do Sys.wait(500);
            let i = i + 1;
        }
        return;
    }
}