class BattleCityGame {
	static Player player;
    static Vector playerStartPosition;
    static List bullets;
    static int globalIndex;

    function void init() {
        do SpriteManager.loadPlayerTankUp();
        do SpriteManager.loadPlayerTankRight();
        let bullets = List.new();
        let playerStartPosition = Vector.new(240, 140);
        return;
    }

	function void createBullet(Vector position, Vector direction) {
        do bullets.add(Bullet.new(position, direction));
        return;
    }

    function void destroyBullet(Bullet bullet) {
        var int bulletIndex;
        let bulletIndex = bullets.indexOf(bullet);
        if (bulletIndex = -1) { return; }
        do bullets.removeAt(bulletIndex);
        let globalIndex = globalIndex - 1;
        do bullet.dispose();
        return;
    }

    function void placeWall(int x, int y, int width, int height) {
        do ColliderFactory.create(BoundingBox.new(x, y, width, height), 2, List.new());
        //TODO: отрисовка спрайта
        do Screen.setColor(1);
        do Screen.drawRectangle(x, y, x + width, y + height);
        do Screen.setColor(0);
        return;
    }

	function void exec() {
        var Bullet bulletBuf;
        do Graphics.fillScreen(0);
        do Screen.setColor(1);

        do BattleCityGame.placeWall(20, 20, 440, 20);
        do BattleCityGame.placeWall(20, 20, 20, 200);
        do BattleCityGame.placeWall(460, 20, 20, 220);
        do BattleCityGame.placeWall(20, 220, 440, 20);
        do BattleCityGame.placeWall(100, 80, 40, 20);

        let player = Player.new(playerStartPosition.clone());
        while (true) {
            if (KeyboardExtension.keyDown() = 81) {do GameCore.setState(1); do BattleCityGame.__clearResources(); return;} // Q

            do CollisionManager.update();
            do player.update();
            let globalIndex = 0;
            while (globalIndex < bullets.size()) {
                let bulletBuf = bullets.at(globalIndex);
                do bulletBuf.update();
                let globalIndex = globalIndex + 1;
            }
        }

		return;
	}

    function void __clearResources() {
        var Bullet bulletBuf;
        do player.dispose();
        while (~(bullets.size() = 0)) {
            let bulletBuf = bullets.at(0);
            do bulletBuf.dispose();
            do bullets.removeAt(0);
        }
        return;
    }
}